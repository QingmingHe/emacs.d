#+TITLE: Starter Kit Python
#+OPTIONS: toc:nil num:nil ^:nil

This is part of the [[file:starter-kit.org][Emacs Starter Kit]].

* Starter kit Python

Support for the Python programming language.

** Jedi Completion

Never used rope, ropemacs, pymacs ... They are ugly too slow. The newly
emerging [[https://github.com/davidhalter/jedi][jedi]] is a better choice, though it is almost impossible to install
manually. Or you can just enable company mode or auto-complete instead of
setup jedi.

To install jedi manually:
#+BEGIN_SRC sh :tangle no
  pip install jedi
  pip install epc
#+END_SRC

#+BEGIN_SRC emacs-lisp
(if (eq 0 (call-process-shell-command "python -c \"import jedi;import epc\""
                                      nil nil nil))
    (setq jedi-is-installed t)
  (setq jedi-is-installed nil))
(when jedi-is-installed
  ;; show doc by popup is too annoying, just show in minibuffer
  (setq jedi:tooltip-method nil)
  (setq jedi:complete-on-dot t)
  (cond ((file-directory-p (expand-file-name ".python-environments" starter-kit-dir))
         nil)
        ((executable-find "jediepcserver.py")
         (setq jedi:server-command
               `(,(executable-find "python") ,(executable-find "jediepcserver.py"))))
        (t
         (setq jedi:server-command
               `(,(executable-find "python")
                 ,(expand-file-name
                   "jediepcserver.py"
                   (file-name-directory (locate-library "jedi"))))))))
#+END_SRC

** Configuration for python mode

+ Python uses indentation instead of braces to structure programs so that it's
  crucial to determine at which level of indentation the cursor is. Use
  highlight-indentation-current-column-mode to fulfil this purpose. Another
  minor mode highlight-indentation-mode is too heavy taste and not use it.
+ I don't whether it's a bug or not python.el integrated with Emacs can't
  create imenu index automatically. I have to set imenu-create-index-function
  myself.
#+BEGIN_SRC emacs-lisp
(defun my-python-conf ()
  (when jedi-is-installed
    (jedi:setup))
  (highlight-indentation-current-column-mode)
  (setq imenu-create-index-function 'python-imenu-create-index)
  (modify-syntax-entry ?_ "w")
  (when (> emacs-minor-version 3)
    (setq prettify-symbols-alist
          '(("!=" . ?≠)
            (">=" . ?≥)
            ("<=" . ?≤)
            ("lambda" . ?λ)))
    (prettify-symbols-mode 1)))
(eval-after-load "python"
  `(progn
     (add-hook 'python-mode-hook 'my-python-conf)))
#+END_SRC

** Use Cython mode
   :PROPERTIES:
   :CUSTOM_ID: cython
   :END:
#+begin_src emacs-lisp
    (add-to-list 'auto-mode-alist '("\\.pyx\\'" . cython-mode))
    (add-to-list 'auto-mode-alist '("\\.pxd\\'" . cython-mode))
    (add-to-list 'auto-mode-alist '("\\.pxi\\'" . cython-mode))
#+end_src

** Set up for Sconstruct
[[http://www.scons.org/][SCons]] is a software construction tool written in python. The /input card/ or
/script/ for SCons is *SConstruct*, which can be treated as Python script.
#+BEGIN_SRC emacs-lisp
(setq auto-mode-alist
      (append '(("SConstruct\\'" . python-mode)
		("SConscript\\'" . python-mode))
              auto-mode-alist))
#+END_SRC

** IPython set up

Activate IPython in ansi-term.
#+BEGIN_SRC emacs-lisp
(defun run-ipython ()
  "Activate IPython shell"
  (interactive)
  ;; Get buffer index
  (setq my-ipython-shell-index 0)
  (while (get-buffer (format "*IPython <%d>*" my-ipython-shell-index))
    (setq my-ipython-shell-index (+ 1 my-ipython-shell-index)))
  ;; Create new ansi term
  (ansi-term "ipython" (format "IPython <%d>" my-ipython-shell-index))
  (switch-to-buffer (format "*IPython <%d>*" my-ipython-shell-index)))
#+END_SRC

Or you can use *M-x C-u run-python* command to run Python interpreter. Here
*C-u* prefix enables you input the Python interpreter such IPython.
#+begin_src emacs-lisp
(setq
 python-shell-interpreter "ipython"
 python-shell-interpreter-args ""
 python-shell-prompt-regexp "In \\[[0-9]+\\]: "
 python-shell-prompt-output-regexp "Out\\[[0-9]+\\]: "
 python-shell-completion-setup-code
 "from IPython.core.completerlib import module_completion"
 python-shell-completion-module-string-code
 "';'.join(module_completion('''%s'''))\n"
 python-shell-completion-string-code
 "';'.join(get_ipython().Completer.all_completions('''%s'''))\n")
#+end_src

** Run Windows Python interpreter in Cygwin platform
   :PROPERTIES:
   :TANGLE:   no
   :END:

Don't do this. You will fall into a pit. If you have to, there are several
ways:
- You are under directory where script to run exists ::
  #+BEGIN_SRC sh
    /path/to/windows/python script.py [args]
  #+END_SRC

- You are not there ::
     #+BEGIN_SRC sh
     cat /path/to/script.py | /path/to/windows/python - [args]
     #+END_SRC
     =-= means python interpreter will read from stdin. Note that you can't do
     this in eshell for that eshell can't handle stdin redirection.

- You are editing the script ::
     Make a symbolic to Windows Python:
     #+BEGIN_SRC sh
     ln -s /path/to/windows/python ~/bin/winpy
     #+END_SRC
     Then define quickrun like [[~/.emacs.d/starter-kit-quickrun.org][this]]. Finally run quickrun.

- You are in dired ::
     Run *M-x dired-open-w32-prog-at-point* or *C-u M-x
     dired-open-w32-prog-at-point* as defined [[~/.emacs.d/starter-kit-dired.org][here]].

There is no perfect way. The first and second are tricky to handle the
path. The quickrun will be confused by *#!/usr/bin/env python* and have no
idea whether to run winpy or python. The path problems also exist for
quickrun. The dired way is perfect only if you don't use
matplotlib. Matplotlib can not create configuration directory for some reason.

** Rst in Python doc
#+begin_src emacs-lisp
(defun indirect-region-py-rst ()
  "Edit doc string in indirect buffer in rst-mode."
  (interactive)
  (let (start end)
    (save-excursion
      (setq start (search-backward-regexp "[\"']\\{3\\}" nil t)))
    (when start
      (setq start (+ start 3)))
    (save-excursion
      (setq end (search-forward-regexp "[\"']\\{3\\}" nil t)))
    (when end
      (setq end (- end 3)))
    (when (and start end)
      (indirect-region start end 'rst-mode))))
(eval-after-load "python"
  `(define-key python-mode-map (kbd "C-c '") 'indirect-region-py-rst))
#+end_src
** Python shell send do what I mean

[[help:python-shell-send-region][python-shell-send-region]] is sometimes strange, don't use it.
#+begin_src emacs-lisp
(defun python-shell-send-dwim (arg)
  "Send region or function or class or buffer or statement to python shell.

- If a piece of region is selected, send the active retion.

- With C-u prefix, send function or class definition.

- With C-u C-u prefix, send the whole buffer.

- If at beginning of class or function definition, send the definition
  structure.

- Finally, try to send the statement at point."
  (interactive "P")
  (let (b0 b1)
    (cond ((region-active-p)
           (python-shell-send-region (region-beginning) (region-end)))
          ((equal arg '(4))
           (python-shell-send-defun (y-or-n-p "Don't send decorators?")))
          ((equal arg '(16))
           (python-shell-send-buffer))
          ((save-excursion
             (beginning-of-line)
             (looking-at "[ \t]*\\(class \\|def \\)"))
           (python-shell-send-defun (y-or-n-p "Don't send decorators?")))
          (t
           (save-excursion
             (setq b0 (progn
                        (python-nav-beginning-of-statement)
                        (point)))
             (setq b1 (progn
                        (python-nav-end-of-statement)
                        (point))))
           (python-shell-send-string
            (buffer-substring-no-properties b0 b1))))))
(eval-after-load "python"
  `(define-key python-mode-map (kbd "C-c C-c") 'python-shell-send-dwim))
#+end_src
