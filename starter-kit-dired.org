#+TITLE: Starter Kit Dired
#+OPTIONS: toc:nil num:nil ^:nil

* Starter Kit Dired
** How dired presents && Dired+
#+BEGIN_SRC emacs-lisp
;; (setq dired-listing-time-style "--time-style long-iso")
(setq dired-listing-time-style "")
(setq dired-listing-switches
      (concat "-lah1v --group-directories-first " dired-listing-time-style))
(setq dired-hide-details-hide-information-lines nil)
(setq dired-hide-details-hide-symlink-targets nil)
(setq diredp-hide-details-initially-flag nil)
(eval-after-load "dired"
  `(progn
     (require 'dired-tar)
     (require 'dired+)))
#+END_SRC

- l: Is the only mandatory one.
- a: Means to list all files.
- h: Human readable sizes, such as M for mebibytes.
- 1v: Affects the sorting of digits, hopefully in a positive way.
- --group-directories-first: self-explanatory, I like to have the directories
  on the top, separate from the files.
- --time-style long-iso: show time like *2015-04-12 10:43*. However
  [[help:dired-move-to-filename-regexp][dired-move-to-filename-regexp]] doesn't recognize it.

I want to know how many files under current directory and human readable
available size. The heading line will not be hided if
[[help:global-dired-hide-details-mode][global-dired-hide-details-mode]] is on.
#+begin_src emacs-lisp
(defun my-count-files-under-dir (dir)
  (let ((file-num 0)
        (dir-num 0)
        (files (directory-files dir nil nil t)))
    (mapc
     (lambda (f)
       (unless (or
                (string= "." f)
                (string= ".." f))
         (if (file-directory-p (expand-file-name f dir))
             (setq dir-num (1+ dir-num))
           (setq file-num (1+ file-num)))))
     files)
    (cons file-num dir-num)))

(defun my-dired-present-heading ()
  (let ((free-size
         (/ (/
             (string-to-int (or (get-free-disk-space ".") "0"))
             1024.0) 1024.0))
        buffer-read-only num file-num dir-num size aline)
    (save-excursion
      (goto-char (point-min))
      (while (re-search-forward
              "^ +\\(/.*\\):\n +total[a-zA-Z ]+\\([0-9\\.]+[KMG]?\\)" nil t)
        (setq num
              (my-count-files-under-dir (match-string-no-properties 1))
              file-num (car num)
              dir-num (cdr num)
              size (match-string-no-properties 2))
        (delete-region (line-beginning-position) (line-end-position))
        (insert (format
                 "  files: %i directories: %i used: %s available: %.1fG \n"
                 file-num dir-num size free-size))))
    (set-buffer-modified-p nil)))

(add-hook 'dired-after-readin-hook #'my-dired-present-heading)
#+end_src

More colorful dired.
#+begin_src emacs-lisp
(with-eval-after-load 'dired
  (require 'dired-rainbow)
  (dired-rainbow-define-chmod executable "Green" "-.*x.*"))
#+end_src

** Basic settings

#+BEGIN_SRC emacs-lisp
;; try to guess destination
(setq dired-dwim-target t)
;; use the same buffer for viewing dir, instead of spawning many
(put 'dired-find-alternate-file 'disabled nil)
;; copy, remove files asynchronous. It's a pitty that `dired-async` don't
;; support tramp well.
;; (eval-after-load "dired-aux"
;;   '(require 'dired-async))
(setq wdired-allow-to-change-permissions t)
#+END_SRC

** Enhanced Dired key bindings

Key bindings should be set after loading dired+.
#+begin_src emacs-lisp
(defun dired-looking-at-permission-flags ()
  "Looking at permission flags at current line in Dired."
  (interactive)
  (save-excursion
    (forward-line 0)
    (re-search-forward (concat "^.\\{0,4\\}" dired-permission-flags-regexp)
                       (line-end-position) t)))

(defun dired-move-to-first-item ()
  "Move to the first item in current Dired buffer."
  (interactive)
  (goto-char (point-min))
  (while (not (dired-looking-at-permission-flags))
    (forward-line))
  (dired-move-to-filename))

(defun dired-move-to-last-item ()
  "Move to the last item in current Dired buffer."
  (interactive)
  (goto-char (point-max))
  (while (not (dired-looking-at-permission-flags))
    (forward-line -1))
  (dired-move-to-filename))

(defun dired-move-to-next-or-first ()
  "Move to the next or first item in current Dired buffer."
  (interactive)
  (forward-line)
  (while (not (dired-looking-at-permission-flags))
    (if (eobp)
        (goto-char (point-min))
      (forward-line)))
  (dired-move-to-filename))

(defun dired-move-to-previous-or-last ()
  "Move to the previous or last item in current Dired buffer."
  (interactive)
  (forward-line -1)
  (while (not (dired-looking-at-permission-flags))
    (if (= (point) (point-min))
        (goto-char (point-max))
      (forward-line -1)))
  (dired-move-to-filename))

(eval-after-load "dired+"
  `(progn
     (define-key dired-mode-map (kbd "RET")
       (lambda ()
         "If file name at current line in Dired is a directory, visit the
directory in the same Dired buffer; otherwise visit the file in another buffer
with the Dired buffer preserved."
         (interactive)
         (let ((file (dired-get-file-for-visit)))
           (if (file-directory-p file)
               (dired-find-alternate-file)
             (find-file file)))))
     (define-key dired-mode-map (kbd "^")
       (lambda ()
         "Goto parent directory in current Dired buffer with cursor locating
at line of last directory."
         (interactive)
         (let ((last-path default-directory))
           (find-alternate-file "..")
           (dired-goto-file last-path))))
     (define-key dired-mode-map (kbd "j")
       (lambda ()
         "Original `dired-goto-file' can't jump to directory in Dired
buffer. This command uses ido to complete read a file or directory name from
all files and directories under current path and jump to it in current Dired
buffer."
         (interactive)
         (let ((fname (helm-comp-read
                       "Goto file or dir: "
                       (directory-files "."))))
           (cond
            ((string= "." fname)
             (goto-char (point-min))
             (re-search-forward " \\.$" nil t)
             (dired-move-to-filename))
            ((string= ".." fname)
             (goto-char (point-min))
             (re-search-forward " \\.\\.$" nil t)
             (dired-move-to-filename))
            (t
             (dired-goto-file (expand-file-name fname)))))))
     (define-key dired-mode-map (kbd "<f5>") 'dired-hide-details-mode)
     (define-key dired-mode-map (kbd "<") #'dired-move-to-first-item)
     (define-key dired-mode-map (kbd ">") #'dired-move-to-last-item)
     (define-key dired-mode-map (kbd "n") #'dired-move-to-next-or-first)
     (define-key dired-mode-map (kbd "T") #'dired-tar-tar-untar)
     (define-key dired-mode-map (kbd "N") #'dired-man-file)
     (define-key dired-mode-map (kbd "p") #'dired-move-to-previous-or-last)
     (define-key dired-mode-map (kbd "C-f") #'scroll-up-command)
     (define-key dired-mode-map (kbd "C-b") #'scroll-down-command)))
#+end_src

** Find name Dired

+ Use [[help:find-name-dired][find-name-dired]] instead of [[help:find-dired][find-dired]] for the former's simplicity.
+ By default Emacs will pass *-exec* to find and that makes it very slow. It is
  better to collate the matches and then use *xargs* to run the command. To do
  this instead add this to your =~/.emacs=:
+ You can refer to [[https://github.com/typester/emacs/blob/master/lisp/find-dired.el][find dired source]] to get the description of
  [[help:find-ls-option][find-ls-option]]. In short, [[help:car][car]] of it is passed to *find* and [[help:cdr][cdr]] is to tell
  Dired how to parse the output.
+ From [[http://stackoverflow.com/questions/14602291/dired-how-to-get-really-human-readable-output-find-ls-option][stackoverflow]] you can get the answer to get human readable size and
  align Dired output. It may be a bug of *find-dired.el*.
#+begin_src emacs-lisp
(setq find-ls-option '("-print0 | xargs -0 ls -alhd" . ""))
#+end_src

** Ido Dired and move to file's line

#+begin_src emacs-lisp
(defun ido-dired-dwim ()
  "Call `ido-dired' and move to last file's line in Dired buffer if possible."
  (interactive)
  (let* ((file-name (buffer-file-name))
         (dir-name (if file-name
                       (expand-file-name (file-name-directory file-name))
                     "")))
    (call-interactively 'ido-dired)
    (when (string= dir-name (expand-file-name default-directory))
      (dired-goto-file file-name))))
(define-key global-map (kbd "C-x d") 'ido-dired-dwim)
#+end_src

** Find by pinyin Dired

From [[http://github.com/redguardtoo/find-by-pinyin-dired][find by pinyin dired]]. The variable [[help:fbpd-char-table][fbpd-char-table]] is defined in
pinyin-search library.
#+begin_src emacs-lisp
(defun find-by-pinyin-dired (dir pattern)
  "Search DIR recursively for files/directories matching the PATTERN,
and run Dired on those files.
PATTERN is sequence of first character of PinYin from Chinese file name."
  (require 'pinyin-search)
  (interactive
   "DFind-name (directory): \nsFind-name (first characters of Hanzi Pinyin): ")
  (let ((regexp ".*") str)
    (dotimes (i (length pattern))
      (setq str (nth (- (aref pattern i) 97) fbpd-char-table))
      (setq regexp (concat regexp str ".*")))
    ;; find-lisp-find-dired is a lisp version
    (find-lisp-find-dired dir regexp)))
#+end_src

** Dired sort

+ Ctime is not the file creation time, it's the inode change time which is
  updated whenever anything about the file changes (contents or metadata)
  except that the file is merely read. Ctime is always more recent than the
  mtime (modification time).
+ To get full information on file, use *stat* command.
+ To sort files by creation time:
  #+begin_src sh :tangle no
  stat -c '%W %n' * | sort -k1n
  #+end_src
#+begin_src emacs-lisp
(defun dired-sort-mtime ()
  (interactive)
  (dired-sort-other (concat "-laht " dired-listing-time-style)))

(defun dired-sort-ctime ()
  (interactive)
  (dired-sort-other (concat "-lahct " dired-listing-time-style)))

(defun dired-sort-access-time ()
  (interactive)
  (dired-sort-other (concat "-lahut " dired-listing-time-style)))

(defun dired-sort-size ()
  (interactive)
  (dired-sort-other (concat "-lahS " dired-listing-time-style)))

(defun dired-sort-extension ()
  (interactive)
  (dired-sort-other
   (concat "-lahX --group-directories-first " dired-listing-time-style)))

(defun dired-sort-name ()
  (interactive)
  (dired-sort-other dired-listing-switches))
#+end_src

** Dired man

#+begin_src emacs-lisp
(defun dired-man-file (file)
  (require 'man)
  (interactive
   (list
    (ido-read-file-name ": " nil (dired-get-file-for-visit))))
  (Man-getpage-in-background file))
#+end_src

** Add dired to recent file

#+begin_src emacs-lisp
(defun recentf-add-dired-file ()
  (require 'recentf)
  (when (eq major-mode 'dired-mode)
    (recentf-add-file dired-directory)))
(add-hook 'dired-mode-hook #'recentf-add-dired-file)
#+end_src

** Open w32 program at point in Dired and eshell

#+BEGIN_SRC emacs-lisp
(defun dired-open-w32-prog-at-point (arg)
  "Open Windows program at point in dired. With prefix, read argument passed
to program from minibuffer."
  (interactive "P")
  (let ((prog-name (dired-get-file-for-visit))
        (prog-argv
         (when arg
           (read-shell-command "Argv passed to prog: "))))
    (when prog-argv
      (setq prog-argv (cygwin-to-win-path prog-argv)))
    (w32-shell-execute "open" prog-name (format "\"%s\"" prog-argv))))

(defun dired-open-nautilus ()
  (interactive)
  (call-process-shell-command "nautilus ."))

(defun dired-open-w32-or-nautilus ()
  (interactive)
  (if (executable-find "nautilus")
      (call-interactively 'dired-open-nautilus)
    (if *cygwin*
        (call-interactively 'dired-open-w32-prog-at-point)
      (user-error "no nautilus or windows"))))

;; eshell alias: alias wf 'eshell-open-w32-prog $*'
(defun eshell-open-w32-prog (args)
  (w32-shell-execute
   "open"
   (expand-file-name (car args))
   (when (cdr args) (mapconcat 'concat (cdr args) " "))
   nil))

(eval-after-load "dired"
  `(define-key dired-mode-map "/" 'dired-open-w32-or-nautilus))
#+END_SRC

** Tips

- *w* copies file name to ring. *C-u 0 w* copies full path.
- *!* run shell command, *&* run shell command synchronously.
- *#* to flag all auto save files for deletion.
- *~* to flag all backup files for deletion.
