#+TITLE: Starter Kit Theme
#+OPTIONS: toc:nil num:nil ^:nil

* Starter Kit Theme

** Highlight indentation

#+begin_src emacs-lisp
(require 'highlight-indentation)
#+end_src

** Emacs Layout

It's not easy to recognize the completion words in horizontally split window
if two windows are already split horizontally. Therefore set Emacs to split
vertically by default.
#+BEGIN_SRC emacs-lisp
(setq split-height-threshold nil)
(setq split-width-threshold 160)
#+END_SRC

** Font for English and Chinese

#+BEGIN_SRC emacs-lisp
(set-face-attribute 'default (selected-frame) :height 120)
(when (window-system)
  (catch 'en-font-found
    (mapc
     (lambda (ft)
       (when (x-list-fonts ft)
         (set-face-attribute
          'default nil :font ft)
         (throw 'en-font-found ft)))
 '("Inconsolata" "Consolas")))
  (catch 'zh-font-found
    (mapc
     (lambda (ft)
       (when (x-list-fonts ft)
         (dolist (charset '(kana han symbol cjk-misc bopomofo))
           (set-fontset-font (frame-parameter nil 'font)
                             charset
                             (font-spec :family ft :size 14)))
         (throw 'zh-font-found ft)))
     '("WenQuanYi Micro Hei Mono" "Microsoft Yahei"))))
#+END_SRC

*** Install font
**** Windows

Open Windows explorer and enter =C:\WINDOWS\fonts= -> file -> install new font
-> click the font file.

**** Linux

+ Install fontconfig.
+ Copy font file to =~/.fonts/=.
+ Shell command: fc-cache ~/.fonts/

*** TODO WenQuanYi

WenQuanYi can be installed by fontconfig but Emacs can't recognize it.
(x-list-fonts "WenQuanYi Micro Hei") returns nil.

** Fringe

Don't show fringe in GUI, it's too ugly.
#+BEGIN_SRC emacs-lisp
(fringe-mode '(4 . 0))
#+END_SRC

** Power line

My powerline theme based on powerline-default-theme.
#+begin_src emacs-lisp
(defun powerline-my-theme ()
  "Setup my mode-line."
  (interactive)
  (setq-default mode-line-format
                '("%e"
                  (:eval
                   (let* ((active (powerline-selected-window-active))
                          (mode-line (if active 'mode-line 'mode-line-inactive))
                          (face2 (if active 'powerline-active1 'powerline-inactive1))
                          (face1 (if active 'powerline-active2 'powerline-inactive2))
                          (separator-left (intern (format "powerline-%s-%s"
                                                          powerline-default-separator
                                                          (car powerline-default-separator-dir))))
                          (separator-right (intern (format "powerline-%s-%s"
                                                           powerline-default-separator
                                                           (cdr powerline-default-separator-dir))))
                          (lhs (list (powerline-raw "%*" face2 'l)
                                     (powerline-buffer-size face2 'l)
                                     (powerline-raw mode-line-mule-info face2 'l)
                                     (powerline-buffer-id face2 'l)
                                     (when (and (boundp 'which-func-mode) which-func-mode)
                                       (powerline-raw which-func-format face2 'l))
                                     (powerline-raw " " face2)
                                     (funcall separator-left face2 face1)
                                     (when (boundp 'erc-modified-channels-object)
                                       (powerline-raw erc-modified-channels-object face1 'l))
                                     (powerline-major-mode face1 'l)
                                     (powerline-process face1)
                                     (powerline-minor-modes face1 'l)
                                     (powerline-narrow face1 'l)
                                     (powerline-raw " " face1)
                                     (funcall separator-left face1 face2)
                                     (powerline-vc face2 'r)))
                          (rhs (list (powerline-raw global-mode-string face2 'r)
                                     (funcall separator-right face2 face1)
                                     (powerline-raw "%4l" face1 'l)
                                     (powerline-raw ":" face1 'l)
                                     (powerline-raw "%3c" face1 'r)
                                     (funcall separator-right face1 face2)
                                     (powerline-raw " " face2)
                                     (powerline-raw "%6p" face2 'r))))
                     (concat (powerline-render lhs)
                             (powerline-fill face2 (powerline-width rhs))
                             (powerline-render rhs)))))))
#+end_src

** Zenburn

#+begin_src emacs-lisp
(load-theme 'zenburn t)
(set-face-background 'highlight-indentation-current-column-face "#4F4F4F")
;; (set-face-attribute 'mode-line nil
;;                       :box nil
;;                       :underline nil
;;                       :inverse-video nil)
;; (set-face-attribute 'mode-line-inactive nil
;;                       :box nil
;;                       :underline nil
;;                       :inverse-video nil)
#+end_src

** Solarized theme
   :PROPERTIES:
   :tangle: no
   :END:

[[https://github.com/bbatsov/solarized-emacs][Solarized theme here]] seems better than [[https://github.com/sellout/emacs-color-theme-solarized][this one]].
#+begin_src emacs-lisp
(defadvice load-theme (around starter-kit-load-theme disable)
  ad-do-it
  (set-face-attribute 'mode-line nil
                      :foreground "cyan"
                      :box nil
                      :underline nil
                      :inverse-video nil)
  (set-face-attribute 'mode-line-inactive nil
                      :box nil
                      :underline nil
                      :inverse-video nil)
  (require 'powerline)
  (powerline-my-theme))

(defun starter-kit-solarized-emacs-setup ()
  ;; make the fringe stand out from the background
  (setq solarized-distinct-fringe-background t)
  ;; not make the modeline high contrast
  (setq solarized-high-contrast-mode-line nil)
  ;; Use less bolding
  (setq solarized-use-less-bold t)
  ;; Use more italics
  (setq solarized-use-more-italic t)
  ;; Use less colors for indicators such as git:gutter, flycheck and similar.
  (setq solarized-emphasize-indicators nil)

  ;; font-size changes
  (setq solarized-scale-org-headlines nil)
  (setq solarized-height-minus-1 0.8)
  (setq solarized-height-plus-1 1.1)
  (setq solarized-height-plus-2 1.15)
  (setq solarized-height-plus-3 1.2)
  (setq solarized-height-plus-4 1.3)

  (setq x-underline-at-descent-line t)

  (ad-enable-advice 'load-theme 'around 'starter-kit-load-theme)
  (ad-activate 'load-theme)

  (load-theme 'solarized-dark t))

(starter-kit-solarized-emacs-setup)
#+end_src
** Old solarized theme
   :PROPERTIES:
   :tangle: no
   :END:

Emacs24 has build in support for saving and loading themes.

A Theme builder is available at http://elpa.gnu.org/themes/ along with
a list of pre-built themes at http://elpa.gnu.org/themes/view.html and
themes are available through ELPA.

Downloaded themes may be saved to the =themes/= directory in the base
of the starter kit which ignored by git.  Once downloaded and
evaluated a theme is activated using the =load-theme= function.

#+BEGIN_SRC emacs-lisp
(color-theme-initialize)
(if (or (not (window-system)) (string= "light" (getenv "EMACS_COLOR_THEME")))
    (progn (color-theme-solarized-light)
           (set-face-background 'highlight-indentation-face "#073642")
           (set-face-background 'highlight-indentation-current-column-face "#eee8d5"))
  (progn (color-theme-solarized-dark)
         (set-face-background 'highlight-indentation-face "#073642")
         (set-face-background 'highlight-indentation-current-column-face "#073642")))
#+END_SRC

Load powerline after color theme is set. If you change the color theme in
running emacs, the expressions as follows should be re-evaluated.
#+BEGIN_SRC emacs-lisp
(require 'powerline)
(set-face-attribute 'mode-line nil
                    :background "dim gray"
                    :foreground "cyan"
                    :inverse-video nil
                    :box nil
                    :underline nil)
(set-face-attribute 'mode-line-inactive nil
                    :background "dim gray"
                    :foreground "light gray"
                    :inverse-video nil
                    :box nil
                    :underline nil)
#+END_SRC
