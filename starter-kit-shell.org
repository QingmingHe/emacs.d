#+TITLE: Starter Kit Shell
#+OPTIONS: toc:nil num:nil ^:nil

This is part of the [[file:starter-kit.org][Emacs Starter Kit]].

* Starter Kit Shell
** Eshell

[[http://www.emacswiki.org/emacs/CategoryEshell][Eshell]] is a great shell.

*** Configurations

#+begin_src emacs-lisp
(setq eshell-cmpl-cycle-completions nil
      eshell-save-history-on-exit t
      eshell-cmpl-dir-ignore "\\`\\(\\.\\.?\\|CVS\\|\\.svn\\|\\.git\\)/\\'"
      eshell-cmpl-ignore-case t
      ;; I'd like to keep all the history
      eshell-history-size 10000
      eshell-hist-ignoredups t
      eshell-scroll-to-bottom-on-output nil
      eshell-scroll-show-maximum-output t)

(defun my-esh-opt ()
  (add-to-list 'eshell-visual-commands "ssh")
  (add-to-list 'eshell-visual-commands "tail")
  (add-to-list 'eshell-command-completions-alist
               '("gunzip" "gz\\'"))
  (add-to-list 'eshell-command-completions-alist
               '("tar" "\\(\\.tar|\\.tgz\\|\\.tar\\.gz\\)\\'"))
  (add-to-list 'eshell-command-completions-alist
               '("unzip" "\\.zip\\'"))
  (add-to-list 'eshell-output-filter-functions 'eshell-handle-ansi-color)
  (add-to-list 'eshell-output-filter-functions 'eshell-postoutput-scroll-to-bottom))

(eval-after-load 'esh-opt
  '(progn
     ;; TODO: for some reason requiring this here breaks it, but
     ;; requiring it after an eshell session is started works fine.
     ;; (require 'eshell-vc)
     (setenv "PAGER" "cat")
     ; (set-face-attribute 'eshell-prompt nil :foreground "turquoise1")
     (add-hook 'eshell-mode-hook ;; for some reason this needs to be a hook
               'my-esh-opt)))
#+end_src

The =eshell= directory holds alias definitions and history
information.  It is much like a =.bashrc= file for those who are
familiar with bash.  This set the value of =eshell-directory-name= to
point to the =eshell= directory in this directory.  The =alias= file
is pre-populated with some generally applicable aliases.

#+begin_src emacs-lisp
  (setq eshell-directory-name
        (expand-file-name "./" (expand-file-name "eshell" starter-kit-dir)))
#+end_src

*** Key bindings

+ Use *C-a* to move to beginning of line of beginning of command line.
+ Use *C-p* to complete eshell history with helm.

#+begin_src emacs-lisp
(defun my-eshell-goto-bottom ()
  (interactive)
  (evil-goto-line)
  (eshell-bol))

(defun my-eshell-prev-prompt ()
  (interactive)
  (forward-line 0)
  (re-search-backward eshell-prompt-regexp)
  (eshell-bol))

(defun my-eshell-next-prompt ()
  (interactive)
  (re-search-forward eshell-prompt-regexp)
  (eshell-bol))

(defun my-eshell-keybinding ()
  (define-key eshell-mode-map "\C-a" #'eshell-bol)
  (define-key eshell-mode-map [remap eshell-pcomplete] #'eshell-ido-or-helm)
  (define-key eshell-mode-map [remap eshell-previous-prompt] #'my-eshell-prev-prompt)
  (define-key eshell-mode-map [remap eshell-next-prompt] #'my-eshell-next-prompt)
  (evil-define-key 'normal eshell-mode-map (kbd "G") #'my-eshell-goto-bottom)
  (evil-define-key 'insert eshell-mode-map (kbd "C-p") 'helm-eshell-history)
  (evil-define-key 'normal eshell-mode-map (kbd "C-p")
    (lambda ()
      (interactive)
      (call-interactively 'evil-append)
      (helm-eshell-history))))
(add-hook 'eshell-mode-hook 'my-eshell-keybinding)
#+end_src

*** Better complete in eshell

Use ido to complete file name and helm for the other.
#+begin_src emacs-lisp
(defun eshell-ido-or-helm ()
  "Use ido to complete file and helm for other in eshell."
  (interactive)
  (or
   (and (fboundp 'my-ido-expand-fname)
        (my-ido-expand-fname))
   (helm-esh-pcomplete)))
#+end_src

*** Eldoc

#+begin_src emacs-lisp
(defadvice eldoc-current-symbol (around eldoc-current-symbol-a activate)
  ad-do-it
  (when (and (not ad-return-value)
             (eq major-mode 'eshell-mode))
    (save-excursion
      (goto-char eshell-last-output-end)
      (let ((esym (eshell-find-alias-function (current-word)))
            (sym (intern-soft (current-word))))
        (setq ad-return-value (or esym sym))))))
(defun eshell-turn-on-eldoc ()
  (eldoc-mode 1))
(add-hook 'eshell-mode-hook 'eshell-turn-on-eldoc)
#+end_src

*** Remove duplicate eshell history

+ Remove duplicates in eshell history ring.
+ Use [[elisp:write-region]] instead of [[elisp:write-file]] for that the previous is
  faster and quiet.
+ This can be realized by set [[elisp:(describe-variable 'eshell-hist-ignoredups)][eshell-hist-ignoredups]] to be t.
#+begin_src emacs-lisp
(defadvice eshell-write-history (after remove-eshell-history-dup-after-write activate)
  (with-temp-buffer
    (insert-file-contents eshell-history-file-name)
    (let ((his-ring
           (delete-dups
            (split-string
             (buffer-substring-no-properties (point-min) (point-max))
             "\n"))))
      (with-temp-buffer
        (mapc
         (lambda (item)
           (unless (string-empty-p item)
             (insert (format "%s\n" item))))
         his-ring)
        (write-region (point-min) (point-max) eshell-history-file-name nil 'no-message)))))
#+end_src

*** Other configuration

#+begin_src emacs-lisp
(defun my-eshell-config ()
  (require 'subr-x)
  (visual-line-mode 1)
  (adaptive-wrap-prefix-mode 1))
(add-hook 'eshell-mode-hook #'my-eshell-config)
#+end_src

*** Tips
    :PROPERTIES:
    :TANGLE:   no
    :END:

**** Multiple eshells
You can create eshell by just *M-x eshell*, or if can run *C-u 2 M-x eshell*
to create a eshell numbered 2. If eshell 2 already exists, you will switch to
it. If *M-x* is bound to *helm-M-x*, run *M-x C-u 2 eshell* instead.

**** Alias

The difference between shell alias and eshell alias is that there should not
be a *=* between alias name and command. Because eshell alias is a compiled
lisp function =eshell/alias= which defines an ALIAS using DEFINITION (two
variables). So define *la* like this:
#+begin_src sh
alias ll 'ls -la $*'
#+end_src

You can also define alias with some variables given:
#+begin_src sh
alias clean-white-spaces 'clean-white-spaces $1 t'
#+end_src

To delete an alias:
#+begin_src sh
alias some-alias
#+end_src

**** Combine Eshell with Helm

Use eshell command on each file marked in =helm-find-files=. Command should
accept one file as argument. Aliases ended with "\\(\\$1\\|\\$\\*\\)$" can be
completed by helm.

**** Pseudo device
#+begin_src sh
echo hello > /dev/clip    # goes to the clipboard
echo hello > /dev/kill    # goes to the kill ring
#+end_src

** Multi eshell

Use =multi-eshell= to handle multiple shells. Use *\-mn* to create new shell or
switch to anther existing shell. Shell doesn't support zsh well, so specify
shell file name to be bash. Don't add =--login= to explicit-bash-args, or
shell will run at =~/= path with the =default-directory= to be that of
previous buffer. This will make path completion problematic.
#+begin_src emacs-lisp
(with-eval-after-load 'shell
  (define-key shell-mode-map (kbd "C-p") 'helm-comint-input-ring))
(setq explicit-shell-file-name (executable-find "bash"))
(setq explicit-bash-args '("--noediting" "-i"))
#+end_src

** Term and ansi-term

In most cases, shell is in my favour. Only when I want to run another app
inside emacs, I will resort to to awkward term or ansi-term.

You can key in command in char mode and copy/paste in line mode. The follow
function and kbd provide easy toggling.

#+BEGIN_SRC emacs-lisp
(defun term-toggle-mode ()
  (interactive)
  (if (term-in-line-mode)
      (term-char-mode)
    (term-line-mode)))
(with-eval-after-load 'term
  (define-key term-mode-map (kbd "C-c C-t") #'term-toggle-mode)
  (define-key term-raw-map (kbd "C-c C-t") #'term-toggle-mode))
#+END_SRC

** Shell script

#+begin_src emacs-lisp
(eval-after-load "sh-script"
  `(define-key sh-mode-map (kbd "C-c C-c") 'sh-send-line-or-region-and-step))
#+end_src

** Clear comint buffer

#+begin_src emacs-lisp
(defun comint-clear-buffer ()
  (interactive)
  (let ((comint-buffer-maximum-size 0))
    (comint-truncate-buffer)))
(defun eshell-clear-buffer ()
  (interactive)
  (let ((inhibit-read-only t))
    (erase-buffer)))
#+end_src
